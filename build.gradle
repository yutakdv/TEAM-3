import com.github.spotbugs.snom.SpotBugsTask

plugins {
    id 'application'
    id 'com.github.spotbugs' version '6.4.4'
    id 'pmd'
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.3'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.3'
}

application {
    mainClass = 'engine.Core'
}

// SpotBugs 설정
spotbugs {
    toolVersion = '4.8.6'
    ignoreFailures = false
    effort = "max"
    reportLevel = "medium"
}

tasks.withType(SpotBugsTask).configureEach {
    reports {
        xml.required.set(true)
        html.required.set(true)
    }
    extraArgs = [
            '-excludeBugs', 'gradle/spotbugs/spotbugs-baseline.xml'
    ]
}

// PMD 설정
pmd {
    toolVersion = "6.55.0"
    ruleSets = [] // 기본 ruleset 비활성화
    ruleSetFiles = files("gradle/pmd/ruleset.xml")
}

tasks.withType(Pmd).configureEach {
    ignoreFailures = true
    reports {
        xml.required.set(false)
        html.required.set(true)
    }
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

sourceSets {
    main {
        java {
            srcDirs = ['src'] // 기존 src 디렉터리 사용
        }
        resources {
            srcDirs = ['res'] // 리소스 폴더 지정
        }
    }
}

tasks.withType(JavaExec).configureEach {
    systemProperty "file.encoding", "UTF-8"
}

tasks.register("qa") {
    dependsOn "spotbugsMain", "pmdMain"
}
check.dependsOn "qa"

tasks.test {
    useJUnitPlatform()
}