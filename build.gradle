import com.github.spotbugs.snom.SpotBugsTask

plugins {
    id 'application'
    id 'com.github.spotbugs' version '6.4.4'
    id 'pmd'
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.3'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.3'
}

application {
    mainClass = 'engine.Core'
}

// PMD 규칙
pmd {
    toolVersion = "6.55.0"
    ruleSetFiles = files("gradle/pmd/ruleset.xml")
    ignoreFailures = true
    reportsDir = layout.buildDirectory.dir("reports/pmd").get().asFile
}

tasks.withType(Pmd).configureEach {
    reports {
        html.required.set(true)
        html.outputLocation = layout.buildDirectory.file('reports/pmd/pmd-report.html').get().asFile
    }
}

// SpotBugs 설정
spotbugs {
    toolVersion = '4.8.6'
    ignoreFailures = false
    effort = "max"
    reportLevel = "medium"

    excludeFilter = file("gradle/spotbugs/spotbugs-exclude.xml")
}

def projectLayout = layout

tasks.withType(SpotBugsTask).configureEach {
    reports {
        xml.required.set(true)
        html.required.set(true)
        html.outputLocation = projectLayout.buildDirectory.file('reports/spotbugs/spotbugs-report.html').get().asFile
    }
    extraArgs = [
            '-excludeBugs', 'gradle/spotbugs/spotbugs-baseline.xml'
    ]
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

sourceSets {
    main {
        java {
            srcDirs = ['src'] // 기존 src 디렉터리 사용
        }
        resources {
            srcDirs = ['res'] // 리소스 폴더 지정
        }
    }
}

tasks.withType(JavaExec).configureEach {
    systemProperty "file.encoding", "UTF-8"
}

tasks.test {
    useJUnitPlatform()
}